# Main build config

target = "sight"
src = "src"

declare_args() {
  enable_pkgconf = false
  debug_build = true
  input_stream = true
  processing_detect = true
  output_disk = true
  output_http = false
}

if (enable_pkgconf) {
  pkgcmd = "//build/pkgconf-wrapper.sh"
  libdep = [
    "gflags",
    "libglog",
    "nlohmann_json",
    "libavformat",
    "libavcodec",
    "libswscale",
    "libavutil",
    "mxnet",
  ]
  if (output_http) {
    libdep += [
      "openssl",
    ]
  }
}

executable("$target") {
  sources = [
    "$src/main.cpp",
    "$src/module.cpp",
    "$src/module.h",
    "$src/pipeline.cpp",
    "$src/pipeline.h",
    "$src/queue.h",
    "$src/slot.cpp",
    "$src/slot.h",
    "$src/input/dummy.cpp",
    "$src/input/dummy.h",
    "$src/processing/dummy.cpp",
    "$src/processing/dummy.h",
    "$src/processing/model/dummy.cpp",
    "$src/processing/model/dummy.h",
    "$src/processing/model/model.cpp",
    "$src/processing/model/model.h",
    "$src/output/dummy.cpp",
    "$src/output/dummy.h",
  ]

  defines = [
    "VERSION=1.0",
  ]
  if (debug_build) {
    defines += [
      "DEBUG",
    ]
  }

  cflags = [
    "-fPIC",
    "-pthread",
  ]
  if (debug_build) {
    cflags += [
      "-O0",
      "-g",
    ]
  } else {
    cflags += [
      "-O2",
    ]
  }
  if (enable_pkgconf) {
    cflags += exec_script("$pkgcmd", ["--cflags"] + libdep, "list lines")
  }

  ldflags = [
    "-lpthread",
  ]
  if (debug_build) {
    ldflags += [
      # "-fsanitize=thread",
      # "-fsanitize=memory",
      # "-fsanitize=address",
      # "-fsanitize=undefined",
    ]
  }
  if (enable_pkgconf) {
    ldflags += exec_script("$pkgcmd", ["--libs"] + libdep, "list lines")
  } else {
    ldflags += [
      "-lexecinfo",
      "-lgflags",
      "-lglog",
      "-lavformat",
      "-lavcodec",
      "-lswscale",
      "-lavutil",
      "-lmxnet",
    ]
  }

  include_dirs = [
    "src",
  ]

  if (input_stream) {
    sources += [
      "$src/input/stream.cpp",
      "$src/input/stream.h",
    ]
    defines += [
      "INPUT_STREAM",
    ]
  }

  if (processing_detect) {
    sources += [
      "$src/processing/detect.cpp",
      "$src/processing/detect.h",
    ]
    defines += [
      "PROCESSING_DETECT",
    ]
  }

  if (output_disk) {
    sources += [
      "$src/output/disk.cpp",
      "$src/output/disk.h",
    ]
    defines += [
      "OUTPUT_DISK",
    ]
  }

  if (output_http) {
    sources += [
      "$src/output/http.cpp",
      "$src/output/http.h",
    ]
    defines += [
      "OUTPUT_HTTP",
    ]
    if (!enable_pkgconf) {
      ldflags += [
        "-lcrypto",
        "-lssl",
      ]
    }
  }
}
